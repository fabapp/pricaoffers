/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.4.1/userguide/tutorial_java_projects.html
 */

apply plugin: 'java'

allprojects  {
  apply plugin: 'jacoco'
  group = 'de.meida'
  version = '0.1.0'
}

task codeCoverageReport(type: JacocoReport) {

    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects 
    subprojects.each {
       sourceSets it.sourceSets.main
    }

    reports {
      xml.enabled true
      html.enabled true
      html.destination "${buildDir}/reports/jacoco"
      csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

subprojects {

  apply plugin: 'jacoco'
  apply plugin: 'java'
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
  tasks.withType(JavaCompile) {
  	options.encoding = 'UTF-8'
  }

  repositories {
    mavenLocal()
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url "https://s3.eu-central-1.amazonaws.com/dynamodb-local-frankfurt/release" }
    maven { url "http://repo.opensourceagility.com/release/" }
  }

  dependencies {
  	testCompile('org.springframework.boot:spring-boot-starter-test')
  }
  /*
  sonarqube {
    properties {
        property "sonar.jacoco.reportPaths", "${buildDir}/reports/jacoco/test.exec"
    }
	}
	*/
}